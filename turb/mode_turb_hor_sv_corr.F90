!MNH_LIC Copyright 2002-2020 CNRS, Meteo-France and Universite Paul Sabatier
!MNH_LIC This is part of the Meso-NH software governed by the CeCILL-C licence
!MNH_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt
!MNH_LIC for details. version 1.
MODULE MODE_TURB_HOR_SV_CORR
IMPLICIT NONE
CONTAINS
      SUBROUTINE TURB_HOR_SV_CORR(D,CST,CSTURB,                      &
                      KRR,KRRL,KRRI,OOCEAN,OCOMPUTE_SRC,             &
                      PDXX,PDYY,PDZZ,PDZX,PDZY,                      &
                      PLM,PLEPS,PTKEM,PTHVREF,                       &
                      PTHLM,PRM,                                     &
                      PLOCPEXNM,PATHETA,PAMOIST,PSRCM,               &
                      PWM,PSVM                                       )
!     ################################################################
!
!
!!****  *TURB_HOT_SV_CORR*  computes subgrid Sv2 and SvThv terms
!!
!!    PURPOSE
!!    -------
!!
!!     see TURB_HOR
!!
!!**  METHOD
!!    ------
!!
!!    EXTERNAL
!!    --------
!!
!!    IMPLICIT ARGUMENTS
!!    ------------------
!!
!!    REFERENCE
!!    ---------
!!
!!    AUTHOR
!!    ------
!!
!!      V. Masson               * Meteo-France *
!!
!!    MODIFICATIONS
!!    -------------
!!   Original  06/11/02
!!      JP Pinty       Feb 20, 2003 Add PFRAC_ICE
!! --------------------------------------------------------------------------
!
!*      0. DECLARATIONS
!          ------------
!
USE MODD_CST, ONLY: CST_t
USE MODD_CONF
USE MODD_CTURB, ONLY : CSTURB_t
USE MODD_DIMPHYEX,   ONLY: DIMPHYEX_t
USE MODD_PARAMETERS
USE MODD_NSV, ONLY : NSV,NSV_LGBEG,NSV_LGEND
USE MODD_LES
USE MODD_BLOWSNOW
!
USE MODI_GRADIENT_M
USE MODI_GRADIENT_U
USE MODI_GRADIENT_V
USE MODI_GRADIENT_W
USE MODI_SHUMAN 
USE MODI_LES_MEAN_SUBGRID
USE MODE_EMOIST, ONLY: EMOIST
USE MODE_ETHETA, ONLY: ETHETA
!
USE MODI_SECOND_MNH
!
IMPLICIT NONE
!
!
!*       0.1  declaration of arguments
!
!
!
TYPE(DIMPHYEX_t),         INTENT(IN)    ::  D
TYPE(CST_t),              INTENT(IN)    ::  CST
TYPE(CSTURB_t),           INTENT(IN)    ::  CSTURB
INTEGER,                  INTENT(IN)    ::  KRR          ! number of moist var.
INTEGER,                  INTENT(IN)    ::  KRRL         ! number of liquid var.
INTEGER,                  INTENT(IN)    ::  KRRI         ! number of ice var.
LOGICAL,                  INTENT(IN)    ::  OOCEAN       ! switch for Ocean model version
LOGICAL,                  INTENT(IN)    ::  OCOMPUTE_SRC ! flag to define dimensions of SIGS and SRCT variables
REAL, DIMENSION(:,:,:),   INTENT(IN)    ::  PDXX, PDYY, PDZZ, PDZX, PDZY 
                                                         ! Metric coefficients
REAL, DIMENSION(:,:,:),   INTENT(IN)    ::  PLM          ! mixing length
REAL, DIMENSION(:,:,:),   INTENT(IN)    ::  PLEPS        ! dissipative length
REAL, DIMENSION(:,:,:),   INTENT(IN)    ::  PTKEM        ! tke
REAL, DIMENSION(:,:,:),   INTENT(IN)    ::  PTHVREF      ! reference Thv
REAL, DIMENSION(:,:,:),   INTENT(IN)    ::  PTHLM        ! potential temperature at t-Delta t
REAL, DIMENSION(:,:,:,:), INTENT(IN)    ::  PRM          ! Mixing ratios at t-Delta t
REAL, DIMENSION(:,:,:),   INTENT(IN)    ::  PLOCPEXNM    ! Lv(T)/Cp/Exnref at time t-1
REAL, DIMENSION(:,:,:),   INTENT(IN)    ::  PATHETA      ! coefficients between 
REAL, DIMENSION(:,:,:),   INTENT(IN)    ::  PAMOIST      ! s and Thetal and Rnp
REAL, DIMENSION(:,:,:),   INTENT(IN)    ::  PSRCM        ! normalized 
                  ! 2nd-order flux s'r'c/2Sigma_s2 at t-1 multiplied by Lambda_3
REAL, DIMENSION(:,:,:),   INTENT(IN)    ::  PWM          ! w at t-1
REAL, DIMENSION(:,:,:,:), INTENT(IN)    ::  PSVM         ! scalar var. at t-1
!
!
!
!*       0.2  declaration of local variables
!
REAL, DIMENSION(SIZE(PSVM,1),SIZE(PSVM,2),SIZE(PSVM,3))       &
                                     :: ZFLX, ZA
!
INTEGER             :: JSV          ! loop counter
INTEGER             :: IKU
!
REAL :: ZTIME1, ZTIME2
!
REAL :: ZCSVD  = 1.2  ! constant for scalar variance dissipation
REAL :: ZCTSVD = 2.4  ! constant for temperature - scalar covariance dissipation
REAL :: ZCQSVD = 2.4  ! constant for humidity - scalar covariance dissipation
!
REAL :: ZCSV          !constant for the scalar flux 
! ---------------------------------------------------------------------------
!
IKU=SIZE(PTKEM,3)
CALL SECOND_MNH(ZTIME1)
!
IF(LBLOWSNOW) THEN
! See Vionnet (PhD, 2012) for a complete discussion around the value of the Schmidt number for blowing snow variables        
   ZCSV= CSTURB%XCHF/XRSNOW 
ELSE
   ZCSV= CSTURB%XCHF
ENDIF
!
DO JSV=1,NSV
!
  IF (LNOMIXLG .AND. JSV >= NSV_LGBEG .AND. JSV<= NSV_LGEND) CYCLE
  !
  ! variance Sv2
  !
  IF (LLES_CALL) THEN
    IF (.NOT. L2D) THEN
      ZFLX(:,:,:) =  ZCSV / ZCSVD * PLM(:,:,:) * PLEPS(:,:,:) *   &
         (  GX_M_M(PSVM(:,:,:,JSV),PDXX,PDZZ,PDZX)**2             &
          + GY_M_M(PSVM(:,:,:,JSV),PDYY,PDZZ,PDZY)**2 )
    ELSE
      ZFLX(:,:,:) =  ZCSV / ZCSVD * PLM(:,:,:) * PLEPS(:,:,:) *   &
            GX_M_M(PSVM(:,:,:,JSV),PDXX,PDZZ,PDZX)**2
    END IF
    CALL LES_MEAN_SUBGRID( -2.*ZCSVD*SQRT(PTKEM)*ZFLX/PLEPS, &
                           X_LES_SUBGRID_DISS_Sv2(:,:,:,JSV), .TRUE. )
    CALL LES_MEAN_SUBGRID( MZF(PWM)*ZFLX, X_LES_RES_W_SBG_Sv2(:,:,:,JSV), .TRUE. )
  END IF
  !
  ! covariance SvThv
  !
  IF (LLES_CALL) THEN
    ZA(:,:,:)   =  ETHETA(D,CST,KRR,KRRI,PTHLM,PRM,PLOCPEXNM,PATHETA,PSRCM,OOCEAN,OCOMPUTE_SRC)
    IF (.NOT. L2D) THEN
      ZFLX(:,:,:)=  PLM(:,:,:) * PLEPS(:,:,:)                                          &
          *  (  GX_M_M(PTHLM,PDXX,PDZZ,PDZX) * GX_M_M(PSVM(:,:,:,JSV),PDXX,PDZZ,PDZX)  &
              + GY_M_M(PTHLM,PDYY,PDZZ,PDZY) * GY_M_M(PSVM(:,:,:,JSV),PDYY,PDZZ,PDZY)  &
             ) * (CSTURB%XCSHF+ZCSV) / (2.*ZCTSVD)
    ELSE
      ZFLX(:,:,:)=  PLM(:,:,:) * PLEPS(:,:,:)                                          &
              * GX_M_M(PTHLM,PDXX,PDZZ,PDZX) * GX_M_M(PSVM(:,:,:,JSV),PDXX,PDZZ,PDZX)  &
              * (CSTURB%XCSHF+ZCSV) / (2.*ZCTSVD)
    END IF
    CALL LES_MEAN_SUBGRID( ZA*ZFLX, X_LES_SUBGRID_SvThv(:,:,:,JSV) , .TRUE.)
    CALL LES_MEAN_SUBGRID( -CST%XG/PTHVREF/3.*ZA*ZFLX, X_LES_SUBGRID_SvPz(:,:,:,JSV), .TRUE. )
    !
    IF (KRR>=1) THEN
      ZA(:,:,:)   =  EMOIST(D,CST,KRR,KRRI,PTHLM,PRM,PLOCPEXNM,PAMOIST,PSRCM,OOCEAN)
      IF (.NOT. L2D) THEN
        ZFLX(:,:,:)=  PLM(:,:,:) * PLEPS(:,:,:)                                                 &
            *  (  GX_M_M(PRM(:,:,:,1),PDXX,PDZZ,PDZX) * GX_M_M(PSVM(:,:,:,JSV),PDXX,PDZZ,PDZX)  &
                + GY_M_M(PRM(:,:,:,1),PDYY,PDZZ,PDZY) * GY_M_M(PSVM(:,:,:,JSV),PDYY,PDZZ,PDZY)  &
               ) * (CSTURB%XCHF+ZCSV) / (2.*ZCQSVD)
      ELSE
        ZFLX(:,:,:)=  PLM(:,:,:) * PLEPS(:,:,:)                                                 &
                * GX_M_M(PRM(:,:,:,1),PDXX,PDZZ,PDZX) * GX_M_M(PSVM(:,:,:,JSV),PDXX,PDZZ,PDZX)  &
                * (CSTURB%XCHF+ZCSV) / (2.*ZCQSVD)
      END IF
      CALL LES_MEAN_SUBGRID( ZA*ZFLX, X_LES_SUBGRID_SvThv(:,:,:,JSV) , .TRUE.)
      CALL LES_MEAN_SUBGRID( -CST%XG/PTHVREF/3.*ZA*ZFLX, X_LES_SUBGRID_SvPz(:,:,:,JSV), .TRUE. )
    END IF
  END IF
!
END DO    ! end loop JSV
!
CALL SECOND_MNH(ZTIME2)
XTIME_LES = XTIME_LES + ZTIME2 - ZTIME1
!
END SUBROUTINE TURB_HOR_SV_CORR
END MODULE MODE_TURB_HOR_SV_CORR

